apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
//apply plugin: 'io.fabric'

android {
    compileSdkVersion compileAndTargetSdkLevel
    buildToolsVersion buildTools

    defaultConfig {
        minSdkVersion minSdkLevel
        targetSdkVersion compileAndTargetSdkLevel
        versionCode versionMajor * 1000000 + versionMinor * 10000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
    }

    signingConfigs {
        release {
            storeFile keyProps["store"] != null ? file(keyProps["store"]) : null
            keyAlias keyProps["alias"] ?: ""
            storePassword keyProps["storePass"] ?: ""
            keyPassword keyProps["pass"] ?: ""
        }
    }

    buildTypes {
        debug {
            versionNameSuffix '_debug_' + gitSha()
            applicationIdSuffix '.debug'
        }
        release {
            if (keyProps["store"] != null) {
                signingConfig signingConfigs.release
            }
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        disable 'RtlHardcoded'
    }

    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }
}

dependencies {
    // Dagger 2
    compile "com.google.dagger:dagger:${daggerVersion}"
    apt "com.google.dagger:dagger-compiler:${daggerVersion}"
    provided "org.glassfish:javax.annotation:10.0-b28"

    compile "com.android.support:appcompat-v7:${supportLibraryVersion}"

    compile "com.google.android.apps.muzei:muzei-api:2.0"
    compile "com.squareup.retrofit2:retrofit:2.0.0"
    compile "com.squareup.retrofit2:converter-gson:2.0.0"
    compile "com.squareup.okhttp3:okhttp:3.2.0"
    compile 'com.squareup.okhttp3:logging-interceptor:3.2.0'
    compile "com.google.code.gson:gson:2.6.2"
    compile "com.jakewharton.timber:timber:4.1.1"
    compile('de.psdev.licensesdialog:licensesdialog:1.8.0') {
        exclude module: 'support-v4'
    }
    compile('com.crashlytics.sdk.android:crashlytics:2.5.5@aar') {
        transitive = true;
    }
}

apply from: '../config/static-analysis.gradle'
